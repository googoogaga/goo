;;;; Copyright (c) 2001 Jonathan Bachrach

(use goo)
(use goo/io/write)
(use eval/ast)
(use eval/ast-linearize)

(dv <out-port> <out-port>)

(dm write-binding (out|<out-port> x|<module-binding> k|<sym>)
  (msg out "MB_%s" (binding-name x)))

(dm write-binding (out|<out-port> x|<module-binding> k|(t= 'global))
  (msg out "GB_%s" (binding-name x)))

(dm write-binding (out|<out-port> x|<module-binding> k|(t= 'predefined))
  (msg out "PB_%s" (binding-name x)))

(dm write-binding (out|<out-port> x|<module-binding> k|(t= 'quotation))
  (msg out "QB(%= %=)" (binding-name x) (binding-value x)))

(dm ast-write (out|<out-port> x|<any> d|<int>)
  (write out x))

(dm ast-write (out|<out-port> x|<module-binding> d|<int>)
  (write-binding out x (binding-kind x)))

(dm ast-write (out|<out-port> x|<local-binding> d|<int>)
  (write out "LB")
  (when (binding-mutable? x)
    (write out "M"))
  ;; (when (binding-dotted? x)
  ;;   (write out "D"))
  (msg out "_%s" (binding-name x)))

(dm ast-write (out|<out-port> x|<local-reference> d|<int>)
  (msg out "LR^%s" (reference-binding x)))

(dm ast-write (out|<out-port> x|<global-reference> d|<int>)
  (msg out "GR^%s" (reference-binding x)))

;; (dm ast-write (out|<out-port> x|<predefined-reference> d|<int>)
;;   (msg out "PR^%s" (reference-binding x)))

(ds (ast-between-parentheses-spaced (,out-val ,depth-val) ,@body)
  (let ((out-var (gensym)) (depth-var (gensym)))
    `(let ((,out-var ,out-val) (,depth-var (+ ,depth-val 1)))
       (put ,out-var #\() 
       ,@(rep loop ((forms '()) (body body) (first? #t))
           (if (empty? body)
               (rev! forms)
               (loop (pair `(ast-write ,out-var ,(head body) ,depth-var)
                           (pair (or first? `(put ,out-var #\space)) 
                                 forms))
                     (tail body) 
                     #f)))
       (put ,out-var #\)))))

(dm ast-write (out|<out-port> x|<local-assignment> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "SET" (assignment-reference x) (assignment-form x)))

(dm ast-write (out|<out-port> x|<global-assignment> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "SET" (assignment-binding x) (assignment-form x)))

(dm ast-write (out|<out-port> x|<definition> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "DEF" (assignment-binding x) (assignment-form x)))

(dm do-ast-write-list 
    (out|<out-port> x prefix|<str> suffix|<str> d|<int> do-first? print)
  (write out prefix)
  (rep loop ((x x) (first? #t))
    (unless (empty? x)
      (when (or (not do-first?) (not first?)) (write out " "))
      (ast-write out (head x) d)
      (loop (tail x) #f)))
  (write out suffix))

(dm ast-write-list (out|<out-port> x|<list> d|<int>)
  (do-ast-write-list out x "(" ")" d #t ast-write))

(dm ast-write (out|<out-port> x|<list> d|<int>)
  (ast-write-list out x "(" ")" d))

(dm ast-write (out|<out-port> x|<ast-function> d|<int>)
  (ast-between-parentheses-spaced (out d)
    (function-kind x) (function-bindings x)
    (function-body x)))

(dm ast-write (out|<out-port> x|<alternative> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "IF" (alternative-condition x) 
    (alternative-consequent x) (alternative-alternant x)))

(dm ast-write (out|<out-port> x|<sequential> d|<int>)
  (do-ast-write-list out x "(SEQ" ")" d #f ast-write))

(dm ast-write (out|<out-port> x|<constant> d|<int>)
  (msg out "C(%=)" (constant-value x)))

(dm ast-write (out|<out-port> x|<regular-application> d|<int>)
  (ast-between-parentheses-spaced (out d)
    (application-function x) (application-arguments x)))

(dm ast-write (out|<out-port> x|<predefined-application> d|<int>)
  (ast-between-parentheses-spaced (out d)
    (application-binding x) (application-arguments x)))

(dm ast-write (out|<out-port> x|<fix-let> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "LET" (map list (fix-let-bindings x) (fix-let-arguments x))
    (fix-let-body x)))

(dm ast-write (out|<out-port> x|<arguments> d|<int>)
  (ast-write-list out x "" "" #f d))

(dm ast-write (out|<out-port> x|<locals> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "LOC" (map list (locals-bindings x) (locals-functions x))
    (locals-body x)))

(dm ast-write (out|<out-port> x|<bind-exit> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "ESC" (bind-exit-main-fun x)))

(dm ast-write (out|<out-port> x|<unwind-protect> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "FIN" (unwind-protect-protected-thunk x) (unwind-protect-cleanup-thunk x)))

(dm ast-write (out|<out-port> x|<monitor> d|<int>)
  (ast-between-parentheses-spaced (out d)
    "MON" (monitor-handler x) (monitor-main-thunk x)))

(dm ast-write (out|<out-port> x|<closure-creation> d|<int>)
  (msg out "CC-%=(F %=)" 
          (closure-creation-index x) (closure-creation-free x)))

(dm ast-write (out|<out-port> x|<free-reference> d|<int>)
  (msg out "FR^%s" (reference-binding x)))

(dm ast-write (out|<out-port> x|<box-creation> d|<int>)
  (msg out "BC(%=)" (box-reference x)))

(dm ast-write (out|<out-port> x|<box-read> d|<int>)
  (msg out "BR(%=)" (box-reference x)))

(dm ast-write (out|<out-port> x|<box-write> d|<int>)
  (msg out "BW(%= %=)" (box-reference x) (box-form x)))

(dm function-kind (x|<ast-function>)
  "F")

;; (dm function-kind (x|<function-definition>)
;;   (msg-to-str "FD-%=" (function-index x)))
;; 
;; (dm function-kind (x|<method-definition>)
;;   "MD")
;; 
;; (dm function-kind (x|<primitive-definition>)
;;   (msg-to-str "PD-%=" (function-name x)))

;; (dm ast-write (out|<out-port> x|<top-level-form> d|<int>)
;;   (msg out "FRM(PG %= QS %= DS %=)" 
;;        (form-program x) (form-quotations x) (form-definitions x)))

;; (dm ast-write (out|<out-port> x|<flattened-program> d|<int>)
;;   (msg out "PG(FM %=\n   QS %=\n   DS %=)" 
;;        (program-form x) (program-quotations x) (program-definitions x)))

