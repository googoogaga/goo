;;;; Copyright 2002, Jonathan Bachrach.  See file TERMS.

(use goo)
(use music/midi)

;;; TONE

(dv <tone-map> <tab>)

;; TODO: SEPARATE OUT TONE-MAP SO IT CAN BE SHARED EASIER

(dv $percussion-channel 9)

(dc <tone> (<any>))
  (dp tone-name (x|<tone> => <sym>))
  (dp tone-key (x|<tone> => <chr>))
  (dp tone-channel (x|<tone> => <int>) $percussion-channel)
  (dp tone-pitch (x|<tone> => <int>))

(dv $zero-tone-key #\+)
(dv $std-tone-map
  (fab-map <tone-map> $zero-tone-key 0))

;;; INSTRUMENT

(dc <instrument> (<any>))
  (dp instrument-name (x|<instrument> => <sym>))
  (dp instrument-version (x|<instrument> => <int>))
  (dp instrument-tone-map (x|<instrument> => <tone-map>) $std-tone-map)

;;; INSTRUMENTS

(dv $instruments (fab <vec> 0))

(dm register-instrument (name version tones)
  (def tone-map (fab <tone-map> (len tones)))
  (def inst     (new <instrument>
                  instrument-name     name
                  instrument-version  version
                  instrument-tone-map tone-map))
  (do (fun (t) (set (elt tone-map (tone-key t)) t)) tones)
  (add! $instruments inst))

(df fab-tone (name key pitch)
  (new <tone> tone-name name tone-key key tone-pitch pitch))

(dv $zero-tone (fab-tone 'zero $zero-tone-key 0))

(ds (def-instrument ,inst-name ,version ,@tones)
  (let ((tone-inits
         (map (fun (tone) 
                (mif ((,name ,key ,pitch) tone) 
                     `(lst ',name ,key ,pitch)))
              tones)))
    `(register-instrument ',inst-name ,version
       (map (fun (tone-args) (app fab-tone tone-args)) (lst ,@tone-inits)))))

(dm find-instrument (name|<sym> => (t? <instrument>))
  (esc ret
    (for ((inst $instruments))
      (when (== (instrument-name inst) name)
        (ret inst)))))

(dm percussion-pitch (name|<str> => (t? <int>))
  (def mi (find-midi-percussion name))
  (and mi (midi-number mi)))

(def-instrument djembe 1
  (bass #\B (percussion-pitch "Hi Bongo"))
  (tone #\T (percussion-pitch "Low Bongo"))
  (slap #\S (percussion-pitch "Bass Drum 1")))

(def-instrument djundjun 1
  (kenkeni  #\S (percussion-pitch "Open Hi Conga"))
  (sangban  #\T (percussion-pitch "Low Conga"))
  (doundoun #\B (percussion-pitch "Low Floor Tom")))

(def-instrument bell 1
  (kengele  #\B (percussion-pitch "Cowbell")))

(def-instrument clave 1
  (clave    #\B (percussion-pitch "Claves")))

(def-instrument metronome 1
  (tick  #\T (percussion-pitch "Side Stick")))

(export 
  <tone-map>
  <tone>
  $zero-tone-key
  $zero-tone
  tone-name
  tone-key
  tone-channel
  tone-pitch
  $std-tone-map
  <instrument>
  instrument-name
  instrument-version
  instrument-tone-map
  find-instrument)
